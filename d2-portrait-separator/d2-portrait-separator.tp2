BACKUP ~weidu_external/backup/d2-portrait-separator~
AUTHOR ~Dan_P~
VERSION ~1.7~


LANGUAGE ~English~
         ~english~
         ~d2-portrait-separator/tra/english/setup.tra~


//Portrait Gender Separator

BEGIN @1
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee iwdee eet~) ~Game not supported~
LABEL ~D2-PORTRAIT-SEPARATOR~


//
//variables
ACTION_IF (GAME_IS ~iwdee~) BEGIN
  OUTER_TEXT_SPRINT game ~iwdee~    // IWDEE
END
ELSE BEGIN
  OUTER_TEXT_SPRINT game ~bgee~     // BG games
END

OUTER_TEXT_SPRINT female     ~%MOD_FOLDER%/%game%(f)~
OUTER_TEXT_SPRINT male       ~%MOD_FOLDER%/%game%(m)~
OUTER_TEXT_SPRINT portraits  ~%USER_DIRECTORY%/portraits~

OUTER_SET infinityui = 0
ACTION_IF (RESOURCE_CONTAINS ~UI.MENU~ ~rgGetGameEnginePortraits~)  BEGIN OUTER_SET infinityui = 1 END



//custom LUA method (no Infinity UI++)
ACTION_IF (infinityui = 0) BEGIN
<<<<<<<< .../temp/M_d2port
function addPortrait(name, gender)
         table.insert(portraits, {name, gender})
end
>>>>>>>>
COPY ~.../temp/M_d2port~  ~override/M_d2port.lua~
END

//
//for Infinity UI++ (prep UI.MENU)
ACTION_IF (infinityui = 1) BEGIN
COPY_EXISTING ~UI.MENU~  ~override~
  REPLACE_TEXTUALLY ~{'MANLEYX', 2},~
                    ~{'MANLEYX', 2},
d2marker~
  REPLACE_TEXTUALLY ~{'2MPAL1_', 1},~
                    ~{'2MPAL1_', 1},
d2marker~
BUT_ONLY
END


//
//functions for writing portrait lists
DEFINE_ACTION_FUNCTION d2portraitF BEGIN

      ACTION_IF (infinityui = 0) BEGIN
        APPEND ~M_d2port.lua~ ~addPortrait('%BASH_FOR_RES%', 2)~
      END

      ACTION_IF (infinityui = 1) BEGIN
        COPY_EXISTING ~UI.MENU~  ~override~
          REPLACE_TEXTUALLY ~d2marker~  
                            ~		{'%BASH_FOR_RES%', 2},
d2marker~
        BUT_ONLY
      END

END

DEFINE_ACTION_FUNCTION d2portraitM BEGIN

      ACTION_IF (infinityui = 0) BEGIN
        APPEND ~M_d2port.lua~ ~addPortrait('%BASH_FOR_RES%', 1)~
      END

      ACTION_IF (infinityui = 1) BEGIN
        COPY_EXISTING ~UI.MENU~  ~override~
          REPLACE_TEXTUALLY ~d2marker~  
                            ~		{'%BASH_FOR_RES%', 1},
d2marker~
        BUT_ONLY
      END

END


////////////////////////////////////////////////////////////

//
//check Portraits folder

ACTION_BASH_FOR ~%portraits%~ ~f.*\.bmp~ BEGIN
  ACTION_IF !(FILE_EXISTS ~%female%/%BASH_FOR_FILE%~) BEGIN
    ACTION_IF !(~%BASH_FOR_FILE%~ STRING_MATCHES_REGEXP ~.*L\.bmp~) BEGIN
      LAF d2portraitF END
    END
    COPY ~%BASH_FOR_FILESPEC%~ ~override~
  END
END

ACTION_BASH_FOR ~%portraits%~ ~m.*\.bmp~ BEGIN
  ACTION_IF !(FILE_EXISTS ~%male%/%BASH_FOR_FILE%~) BEGIN
    ACTION_IF !(~%BASH_FOR_FILE%~ STRING_MATCHES_REGEXP ~.*L\.bmp~) BEGIN
      LAF d2portraitM END
    END
    COPY ~%BASH_FOR_FILESPEC%~ ~override~
  END
END


//
//check mod folders
  
ACTION_BASH_FOR ~%female%~ ~.*\.bmp~ BEGIN
  ACTION_IF !(~%BASH_FOR_FILE%~ STRING_MATCHES_REGEXP ~.*L\.bmp~) BEGIN
    LAF d2portraitF END
  END
  COPY ~%BASH_FOR_FILESPEC%~ ~override~
END
  
ACTION_BASH_FOR ~%male%~ ~.*\.bmp~ BEGIN
  ACTION_IF !(~%BASH_FOR_FILE%~ STRING_MATCHES_REGEXP ~.*L\.bmp~) BEGIN
    LAF d2portraitM END
  END
  COPY ~%BASH_FOR_FILESPEC%~ ~override~
END

////////////////////////////////////////////////////////////


//clean up
COPY_EXISTING ~UI.MENU~  ~override~
  REPLACE_TEXTUALLY ~d2marker~  ~~
BUT_ONLY

PRINT @3    // reminder to remove files from the Portraits folder beginning in M or F
PRINT @4


/*
*/